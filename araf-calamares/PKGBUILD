pkgname=araf-calamares
_pkgname=calamares
pkgver=3.3.14.r25.g95aa33f
pkgrel=1
pkgdesc='Distribution-independent installer framework - latest git version'
arch=('i686' 'x86_64')
url="https://github.com/calamares/calamares"
license=('LGPL')
conflicts=('calamares-next' 'calamares-git')
provides=('calamares')
depends=(
	'boost-libs'
	'ckbcomp'
	'cryptsetup'
	'doxygen'
	'efibootmgr'
	'gptfdisk'
	'gtk-update-icon-cache'
	'icu'
	'kconfig'
	'kcoreaddons'
	'kcrash'
	'ki18n'
	'kparts'
	'kpmcore'
	'kservice'
	'kwidgetsaddons'
	'libpwquality'
	'mkinitcpio-openswap'
	'polkit-qt6'
	'rsync'
	'qt6-declarative'
	'solid'
	'squashfs-tools'
	'yaml-cpp'
)

makedepends=(
	'boost'
	'cmake'
	'extra-cmake-modules'
	'git'
	'fakeroot'
	'ninja'
	'python-jsonschema'
	'python-pyaml'
	'python-unidecode'
	'qt6-tools'
)
backup=('usr/share/calamares/modules/bootloader.conf'
        'usr/share/calamares/modules/displaymanager.conf'
        'usr/share/calamares/modules/initcpio.conf'
        'usr/share/calamares/modules/unpackfs.conf')

source=("calamares::git+https://github.com/calamares/calamares"
	"araf-calamares.desktop"
	"calamares-_polkit"
	"49-nopasswd-calamares.rules"
	"calamares-wrapper")

sha512sums=('SKIP'
            '50d01c82eb7e95d94254633ac8906b9cd19504f37dcee2b4b2e91487d5676b5f711fccbf41b0a0e701cfa81aceaa0283f4b4eab3dc9b6fdf48e784e5656624b9'
            'e6ee5f1e6cfd9e1168b5d1cf3e3e74411fdd43f8b62339d1d2f71a613751fd67aeb6b0fc44b21a44270c7363298bac7e66de2d1754b3ca2e6aad759691a8fa8d'
            '721fd8eaed5ab65e1d097609a01d2451a47c13e9ae61182a4f4eb9f0e33252f648d499693fd86c60ffc9a3877a0a9007ad016c8b9dca751e4cb60b56b322b04f'
            '34d189f66989c8eb7b6f2ebcd35864942ba1011982f0b18e13bd2ec516cba6f6d7625b499a034f9bc246e03f687207d45071d359403e61646efda2269ae8894c')

prepare() {
	cp -rv ../modules/* "$srcdir/$_pkgname/src/modules/"

	sed -i -e 's/"Install configuration files" OFF/"Install configuration files" ON/' "$srcdir/$_pkgname/CMakeLists.txt"
	sed -i -e "s/desired_size = 512 \* 1024 \* 1024  \# 512MiB/desired_size = 512 \* 1024 \* 1024 \* 16  \# 8589MiB/" "$srcdir/$_pkgname/src/modules/fstab/main.py"
}

pkgver() {
	cd "$srcdir/$_pkgname"
	git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/$_pkgname"

	cmake -S . -B build \
		-G Ninja \
		-DCMAKE_BUILD_RPATH_USE_ORIGIN=ON \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DWITH_APPSTREAM=OFF \
		-DWITH_PYBIND11=OFF \
		-DWITH_QT6=ON \
		-DSKIP_MODULES="dracut \
			dracutlukscfg \
			dummycpp \
			dummyprocess \
			dummypython \
			dummypythonqt \
			initramfs \
			initramfscfg \
			interactiveterminal \
			keyboardq \
			license \
			localeq \
			oemid \
			packagechooserq \
			partitionq \
			services-openrc \
			summaryq \
			tracking \
			usersq \
			welcomeq"

	cmake --build build
}

package() {
	cd "$srcdir/$_pkgname/build"
	DESTDIR="$pkgdir" cmake --install .

	install -Dm644 "$srcdir/araf-calamares.desktop" "$pkgdir/usr/share/applications/araf-calamares.desktop"
	install -Dm755 "$srcdir/araf-calamares.desktop" "$pkgdir/home/liveuser/Desktop/araf-calamares.desktop"
	chmod +x "$pkgdir/home/liveuser/Desktop/araf-calamares.desktop"

	install -Dm644 "$srcdir/calamares-wrapper" "$pkgdir/usr/local/bin/calamares-wrapper"
	chmod +x "$pkgdir/usr/local/bin/calamares-wrapper"
	install -Dm755 "$srcdir/calamares_polkit" "$pkgdir/usr/bin/calamares_polkit"
	install -Dm644 "${srcdir}/49-nopasswd-calamares.rules" "$pkgdir/etc/polkit-1/rules.d/49-nopasswd-calamares.rules"
	rm "$pkgdir/usr/share/applications/calamares.desktop"
}
